plugins {
	id 'java-library'
	id 'maven-publish'
	id 'signing'
}

sourceCompatibility = 1.11

def baseVersion = '3.9.4'
def baseGroupId = 'io.github.adempiere'
def grpcVersion = '1.62.2'
def protobufVersion = '3.25.3'

repositories {
    mavenLocal()
    mavenCentral()
}

allprojects {
    tasks.withType(Javadoc) {
    	options.addStringOption('Xdoclint:none', '-quiet')
   	}
}

dependencies {
   api fileTree(
        dir: 'lib',
        include: [
            '*.jar'
        ]
    )
	api "io.grpc:grpc-netty-shaded:${grpcVersion}"
	// https://central.sonatype.com/artifact/com.google.protobuf/protobuf-java
	implementation "com.google.protobuf:protobuf-java:${protobufVersion}"
	implementation "com.google.protobuf:protobuf-java-util:${protobufVersion}"
	api 'io.jsonwebtoken:jjwt-api:0.11.2'
    api 'io.jsonwebtoken:jjwt-impl:0.11.2'
    api 'io.jsonwebtoken:jjwt-jackson:0.11.2'

	//	ADempiere Core
	api "${baseGroupId}:base:${baseVersion}"

	// ADempiere Projects with additional features
	// Third part access using JWT (https://central.sonatype.com/artifact/io.github.adempiere/adempiere-jwt-token)
	api "${baseGroupId}:adempiere-jwt-token:1.0.2"
}

sourceSets {
    main {
         java {
            srcDirs = ['src/main/java']
         }
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}


def entityType = 'D'
group = "io.github.adempiere"
version = findProperty("deployVersion") ?: "local-1.0.0"

jar {
	manifest {
		attributes(
			"Implementation-Title": "ADempiere gRPC Utils",
			"Implementation-Version": version,
			"EntityType": entityType
		)
    }
}

publishing {
    repositories {
		mavenLocal()

        maven {
            url = findProperty("deployPublishUrl") ?: System.properties['deploy.publish_url']
            credentials {
                username = findProperty("deployUsername") ?: System.properties['deploy.user']
                password = findProperty("deployPassword") ?: System.properties['deploy.token']
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
			groupId = group
			artifactId = 'adempiere-grpc-utils'
			version = version
           	from components.java
           	pom {
                name = 'ADempiere gRPC Utils'
                description = 'Utils classes for start a gRPC easy using ADempiere'
                url = 'http://adempiere.io/'
                licenses {
                    license {
                        name = 'GNU General Public License, version 2'
                        url = 'https://www.gnu.org/licenses/old-licenses/gpl-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'yamelsenih'
                        name = 'Yamel Senih'
                        email = 'ysenih@erpya.com'
                    }
					developer {
						id = 'edwinbetanc0urt'
						name = 'Edwin Betancourt'
						email = 'EdwinBetanc0urt@outlook.com'
					}
                }
                scm {
                    connection = 'scm:git:git://github.com/adempiere/adempiere-grpc-utils.git'
                    developerConnection = 'scm:git:ssh://github.com/adempiere/adempiere-grpc-utils.git'
                    url = 'http://github.com/adempiere/adempiere-grpc-utils'
                }
            }
		}
	}
}


task cleanBuildPublishLocal(type: GradleBuild) {
	tasks = ['clean', 'build', 'publishToMavenLocal']
}


signing {
	def isReleaseVersion = !version.toString().startsWith("local") &&
		!version.toString().endsWith("SNAPSHOT") &&
		version.toString() != "ci"

	sign configurations.archives

	setRequired {
		// signing is required if this is a release version and the artifacts are to be published
		// do not use hasTask() as this require realization of the tasks that maybe are not necessary
		isReleaseVersion && gradle.taskGraph.allTasks.any {
			it.equals(PublishToMavenRepository)
		}
	}
	def signingKey = findProperty("deploySigningKey")
	def signingPassword = findProperty("deploySigningPassword")
	useInMemoryPgpKeys(signingKey, signingPassword)
	sign publishing.publications.mavenJava
}
